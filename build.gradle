plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'net.toshimichi'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version

            from components.java

            pom {
                name = rootProject.name
                description = 'A reactive library for Java'
                url = 'https://github.com/Toshimichi0915/Indexer'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Toshimichi0915'
                        name = 'Toshimichi'
                        email = 'main@toshimichi.net'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:Toshimichi0915/Indexer.git'
                    developerConnection = 'scm:git@github.com:Toshimichi0915/Indexer.git'
                    url = 'https://github.com/Toshimichi0915/Indexer'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'release'
            url = new URL('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }

        maven {
            name = 'snapshot'
            url = new URL('https://s01.oss.sonatype.org/content/repositories/snapshots/')
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications
}
